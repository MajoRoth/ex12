// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {
    /** Initializes the class. */
    static int heapBase;
    static Array memory;
    static Array freeList;
    function void init() {
        // Pseudocode:
        // 1. freeList = heapBase
        // 2. freeList.length = heapLength
        // 3. freeList.next = null
        let memory = 0;
        let freeList = 2048;
        let freeList[0] = 14335; // heapLength
        let freeList[1] = 0;
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return memory[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let memory[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. Assume size > 0. */
    function int alloc(int size) {
        // Pseudocode:
        // 1. Search freeList using best-fit or first-fit heuristics to obtain
        //    a segment with segment.length > size.
        //    If no such segment is found, return -1.
        // 2. block = needed part of the found segment (or all of it, if the 
        //            segment remainder is too small).
        // 3. block[-1] = size + 1 // Remember block size, for de-allocation
        // 4. Update freeList to reflect the allocation
        // 5. Return block
        // The figure MemoryTest/MemoryFreeListExample.png illustrates how
        // the freeList should look like.
        var Array block, next, last;
        let block = freeList;
        let last = 0;
        while(~(block = 0)){
            if(block[0] > size){
                if(block[0]-size-1 > 2){
                    let block[size + 1] = block[0] - size - 1; // set size of next block
                    let block[size + 2] = block[1]; // set next of next block
                    let block[0] = size + 1;
                    if(last = 0){
                        let freeList = block + size + 1;
                    }else{
                        let last[1] = block + size + 1;
                    }
                }else{
                    if(last = 0){
                        let freeList = block[1];
                    }else{
                        let last[1] = block[1];
                    }
                }
                return block + 1;
            }
            let next = block[1];	
            let last = block;
			let block = next;
        }
        return -1;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        // Pseudocode:
        // 1. segment = o - 1
        // 2. Insert segment into the freeList
        var Array segment, block;
        let segment = o - 1;
        let segment[1] = freeList; // nullifying next object
        let freeList = segment;
        return;
    }    

    /** Returns the maximal element contained in the given Array/object. 
     *  Assume inputs are initialized and contain at least one element. */
    function int max(Array o) {
        var int length, max_val, i;
        let length = o[-1];
        let max_val = o[0];
        let i = 1;
        while(i < length-1){
            if(max_val < o[i]){
                let max_val = o[i];
            }
            let i = i + 1;
        }
        return max_val;
        // Hint: the algorithm you need to implement in Memory.alloc saves the
        // size of the allocated block in the memory cell right before the 
        // start of the block, and this can be used to implement Memory.max.
    }
}
